plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"luacraft" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "org.luaj:luaj-jse:3.0.1"

	// JUnit Jupiter API for writing tests
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
	// JUnit Jupiter Engine to run the tests
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

// Use JUnit Platform for running tests
test {
    useJUnitPlatform()
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}
}

task copyExamples(type: Copy) {
	from 'examples'
	into 'run/world/datapacks'
}

tasks.named('runServer').configure {
	dependsOn copyExamples
}